plugins {
    id 'org.springframework.boot' version '2.5.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'   //버전을 자동으로 관리해준다. -> 변수에 자동으로 맞는 버전을 주입
    //Jooq 설정을 쉽게하기 위한 플러그인
    id 'nu.studer.jooq' version '5.2.2' //6.0.1 -> jooq.3.15 -> spring.dependency-management 에러 -> 3.14
    id 'java'
}

group = 'com.uno'
version = '0.1.2-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}
//Jooq 버전 주입을 위한 설정
project.ext {
    querydslVersion = dependencyManagement.importedProperties['querydsl.version']
    jooqVersion = dependencyManagement.importedProperties['jooq.version']
    mysqlVersion = dependencyManagement.importedProperties['mysql.version']
}

dependencies {
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.data:spring-data-rest-hal-explorer'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'mysql:mysql-connector-java'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'


    // queryDSL 설정
    implementation "com.querydsl:querydsl-jpa"
    implementation "com.querydsl:querydsl-core"
    implementation "com.querydsl:querydsl-collections"
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa" // querydsl JPAAnnotationProcessor 사용 지정
    annotationProcessor "jakarta.annotation:jakarta.annotation-api" // java.lang.NoClassDefFoundError (javax.annotation.Generated) 발생 대응

    // Jooq 의존성
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    jooqGenerator "mysql:mysql-connector-java:${project.mysqlVersion}"
}

test {
    useJUnitPlatform()
}

// querydsl 적용
def generated='src/main/generated'

// java source set 에 querydsl QClass 위치 추가
sourceSets {
    main.java.srcDirs += [ generated ]
}

// querydsl QClass 파일 위치를 잡아주는 설정
tasks.withType(JavaCompile) {
    options.getGeneratedSourceOutputDirectory().set(file(generated))
}

// gradle clean 시에 QClass 디렉토리 삭제
clean {
    delete file(generated)
}
// Jooq 설정부
//: gradle groovy 레퍼런스 설정
def generatedJooq = 'src/main/generated-jooq'

jooq {
    //extra property에 접근하기 위한 gradle 문법
    version = project.jooqVersion

    configurations {
        main {  // name of the jOOQ configuration
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'com.mysql.cj.jdbc.Driver'
                    url = 'jdbc:mysql://localhost:3306/getinline'
                    user = 'mino'
                    password = '1234'
                }
                generator {
                    //Generator이름 DefualtGenerator -> JavaGenerator
                    name = 'org.jooq.codegen.JavaGenerator'

                    //데이터베이스 설정
                    database {
                        //jooq에서 제공하는 사용하는 데이터베이스 이름
                        name = 'org.jooq.meta.mysql.MySQLDatabase'
                        inputSchema = 'getinline'
                        //테이블 규칙을 정한다. -> 포함 테이블, 제외 테이블
                        includes = '.*'
                        excludes = ''
                        //엔티티안에 사용하는 데이터타입 중 변환이 필요한 타입 : Enum, LocalDateTime
                        forcedTypes {
                            forcedType {
                                userType = 'com.uno.getinline.constant.EventStatus'
                                enumConverter = true
                                includeExpression = '.*\\.event_status'
                                includeTypes = '.*'
                            }
                            forcedType {
                                userType = 'com.uno.getinline.constant.PlaceType'
                                enumConverter = true
                                includeExpression = '.*\\.place_type'
                                includeTypes = '.*'
                            }
                        }
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                        //자바 8의 LocalDateTime -> 자동 변환 수행하는 옵션
                        javaTimeTypes = true
                    }
                    target {
                        //현재 프로젝트 패키지의 루트 경로
                        packageName = 'com.uno.getinline'
                        //직접 지정할 디렉토리 이름
                        directory = generatedJooq
                    }
                    //생성 이름 전략은 디폴트 사용
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

// incremental build (Grade에서 제공하는 증분 빌드) - Jooq 오브젝트 생성 퍼포먼스 향상
//: Generated 폴더에 빌드할 때마다 처음부터 빌드하게 되는데, Update만 수행하도록 -> 변경점만 추가로 빌드
tasks.named('generateJooq').configure {
    allInputsDeclared = true
    outputs.cacheIf { true }
}
